(*
Chatbox
=======
The chatbox file holds functions and procedures that are used in the
Runescape chat box.
*)

(*
type TRSChatbox
~~~~~~~~~~~~~~~
The type that holds functions and properties of the Runescape chat box interface.
*)
type 
  EChatTab = (ctAll, ctGame, ctPublic, ctPrivate, ctClan, ctTrade);

  TRSChatbox = record(TInterfaceBase)
    FLines: TBoxArray;
    FTabs: TBoxArray;
  end;

(*
var chatbox
~~~~~~~~~~~
Variable that stores functions and properties of the Runescape chatbox interface.
*)
var
  Chatbox: TRSChatbox;

(*
const CHATBOX_COLORS
~~~~~~~~~~~
constant that holds all the possible text-colors
*)
const
  CHATBOX_COLORS = [0, $7F, $FF0000, $7F007F];

{$IFNDEF CODEINSIGHT}
procedure TRSChatbox.__Setup();
var i:Int32;
begin
  for i := 0 to 7 do
    FLines.Append([10,344+(14*i),496,360+(14*i)+1]);
  FLines.Append([10,458,500,474]); //input-field

  FTabs := Grid(6, 1, 50, 15, 66, 0, Point(33, 490));

  with Self do
  begin
    FBounds := [7,345,512,474];
    FName := 'Chatbox';
    FStatic := True;
  end;
end;
{$ENDIF}


(*
chatbox.GetTextOnLine
~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSChatbox.GetTextOnLine(line: Int32; colors:TIntegerArray = CHATBOX_COLORS): String;

Returns the text at the given line ``[0..8]``. 8 is the last line (the input-field)

.. note:: by slacky

Example:
.. code-block:: pascal

    // returns the text at line 6
    chatbox.GetTextOnLine(6);
*)
function TRSChatbox.GetTextOnLine(line: Int32; colors:TIntegerArray = CHATBOX_COLORS): String;
var
  i,bmp: Int32;
  textArr,temp: TPointArray;
  B: TBox := Self.FLines[line];
begin
  for i:=0 to High(colors) do
  begin
    FindColorsTolerance(temp, colors[i], B.x1,B.y1,B.x2,B.y2, 0);
    textArr := CombineTPA(textArr, temp);
  end;
  if Length(textArr) = 0 then 
    Exit;

  OffsetTPA(textArr, [-B.x1,-B.y1]);
  BMP := CreateBitmap(B.x2-B.x1+1,B.y2-B.y1+2);
  DrawTPABitmap(BMP,textArr,255);

  Result := OCR.Recognize(BitmapToMatrix(bmp),TCompareRules([255]), SmallFont);
  FreeBitmap(bmp);
end;

(*
chatbox.FindTextOnLines
~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

    function TRSChatbox.FindTextOnLines(txt: TStringArray; lines: TIntegerArray; colors:TIntegerArray = CHATBOX_COLORS): boolean;

Returns true if the text in 'txt' is found on the 'lines' of the chatbox.  The
lines start with 0 being the bottom chat line.

.. note:: by slacky

Example:

.. code-block:: pascal

    // returns true if "Hello" is found on the first 4 lines
    chatbox.findTextOnLines(['Hello'], [0..3]);
*)
function TRSChatbox.FindTextOnLines(txt: TStringArray; lines: TIntegerArray; colors:TIntegerArray = CHATBOX_COLORS): Boolean;
var
  i: Int32;
begin
  for i:=0 to High(lines) do
    if IsArrInStr(txt, Self.GetTextOnLine(lines[i], colors)) then
      Exit(True);
end;


(*
chatbox.GetLastMessage
~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSChatbox.GetLastMessage(colors:TIntegerArray = CHATBOX_COLORS): String;

Returns the text at the last line in the chatbox

.. note:: by slacky
*)
function TRSChatbox.GetLastMessage(colors:TIntegerArray = CHATBOX_COLORS): String;
begin
  Result := Self.GetTextOnLine(High(Self.FLines)-1, colors);
end;


(*
chatbox.GetChatArea
~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

    function TRSChatbox.GetChatArea(): TBox;

Returns a TBox which only covers the chat-area, does not include the scrollbar and input-field.

.. note:: by slacky
*)
function TRSChatbox.GetChatArea(): TBox;
begin
  with self.FBounds do
    result := [x1, y1, x2-16, y2-16];
end;


(*
chatbox.ClickContinue
~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

    function TRSChatbox.ClickContinue(): Boolean;

Clicks the "Click here to continue"-text in the chatbox.
Returns True if the text was found.

.. note:: by slacky
*)
function TRSChatbox.ClickContinue(): Boolean;
var
  tpa:Array [0..2] of TPoint;
begin
  //match each word sepparatly to avoid whitespace-issues.
  Result := (srl.FindText(tpa[0], 'Click',   'CharsNPC07', self.FBounds) and
             srl.FindText(tpa[1], 'here',    'CharsNPC07', self.FBounds) and
             srl.FindText(tpa[2], 'continue','CharsNPC07', self.FBounds));
  if Result then
  begin
    tpa[2].y += 15;
    mouse.Click(GetTPABounds(tpa), mouse_left);
    Exit();
  end;
end;


(*
chatbox.HandleLevelUp
~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

    function TRSChatbox.HandleLevelUp(): Boolean;

Clicks the "Click here to continue" if text containing `Congratulations` was found

.. note:: by olly
*)
function TRSChatbox.HandleLevelUp(): Boolean;
const
  C_CHAR_POS: TPoint = [107, 383];
  TEXT_COL = 8388608;
var
  p: TPoint;
begin
  if (GetColor(C_CHAR_POS) = TEXT_COL) then
    if (srl.FindText(p, 'Congratulations', 'CharsNPC07', TEXT_COL, 0, Self.FBounds)) then
      Exit(Self.ClickContinue());
end;

type
  TChatOption = record
    Text: String;
    b: TBox;
 end;

function TRSChatbox.GetOptions(TextColors: TIntegerArray = [clBlack, clWhite]): array of TChatOption;
var
  TPA: TPointArray;
  ATPA: T2DPointArray;
  i, j: Integer;
  b: TBox;
  s: String;
begin
  SetLength(ATPA, Length(TextColors));
  for i := 0 to High(ATPA) do
    FindColors(ATPA[i], TextColors[i], Self.FBounds);
  TPA := MergeATPA(ATPA);

  if (Length(TPA) < 10) then
    Exit;

  ATPA := ClusterTPAEx(TPA, 15, 1);
  for i := 0 to High(ATPA) do
  begin
    b := GetTPABounds(ATPA[i]);
    b.Expand(1);
    for j := 0 to High(TextColors) do
    begin
      s := OCR.Recognize(b, TCompareRules([TextColors[j]]), NPCFont);
      if (s <> '') then
        Result += TChatOption([s, GetTPABounds(ATPA[i])]);
    end;
  end;
end;

function TRSChatbox.ClickOption(const Text: TStringArray; TextColors: TIntegerArray = [clBlack, clWhite]): Boolean;
var
  opts: array of TChatOption;
  i: Integer;
begin
  Result := False;

  opts := Self.GetOptions(TextColors);
  for i := 0 to High(opts) do
  begin
    if (IsArrInStr(Text, opts[i].Text)) then
    begin
      Mouse.Click(opts[i].b, mouse_Left);
      Exit(True);
    end;
  end;
end;

function TRSChatbox.ClickOption(const Text: TStringArray; WaitTime: Integer; TextColors: TIntegerArray = [clBlack, clWhite]): Boolean; overload;
var
  t: UInt64;
begin
  t := GetTickCount64() + (WaitTime + Random(200));
  repeat
    if (Self.ClickOption(Text, TextColors)) then
      Exit(True);
    Wait(Random(50, 150));
  until (GetTickCount64() >= t);
end;

function TRSChatbox.PleaseWait(): Boolean;
var
  tpa:Array [0..1] of TPoint;
  t: UInt64;
begin
  Result := False;
  t := GetTickCount64() + Random(1500, 2000);

  while (not Result) and (t > GetTickCount64()) do
  begin
    Result := (srl.FindText(tpa[0], 'Please',   'CharsNPC07', self.FBounds) and
               srl.FindText(tpa[1], 'wait',    'CharsNPC07', self.FBounds));
    Wait(Random(50, 100));
  end;

  if (Result) then
  begin
    t := GetTickCount64() + Random(4000, 5000);
    while (t > GetTickCount64()) do
    begin
      if not (srl.FindText(tpa[0], 'Please',   'CharsNPC07', self.FBounds) and
              srl.FindText(tpa[1], 'wait',    'CharsNPC07', self.FBounds)) then
        begin
          Wait(Random(300, 500));
          Exit;
        end;

      Wait(Random(50, 100));
    end;
  end;
end;

function TRSChatbox.ChatToOption(Option: TStringArray; ClickOption: Boolean; TextColors: TIntegerArray = [clBlack, clWhite]): Boolean;
var
  opts: array of TChatOption;
  i, c: Integer;
  t: UInt64;
  b: Boolean = False;
begin
  Result := False;

  repeat
    opts := Self.GetOptions(TextColors);
    for i := 0 to High(opts) do
    begin
      if (IsArrInStr(Option, opts[i].Text)) then
      begin
        if (ClickOption) then
          Mouse.Click(opts[i].b, mouse_Left);
        Exit(True);
      end;
    end;

    t := GetTickCount64() + Random(1000, 1500);
    while (t > GetTickCount64()) do
    begin
      if (ClickContinue()) then
      begin
        b := True;
        Break;
      end else
        b := False;

      Wait(Random(50, 100));
    end;

    if (b = False) then
      Exit(False);

    self.PleaseWait();
    Wait(Random(150, 300));
  until (not srl.isLoggedIn());
end;

function TRSChatbox.GetDisplayName(): String;
var
  s: String;
  p: Integer;
begin
  s := Self.GetTextOnLine(High(Self.FLines), [$000000]);
  if ((p := Pos(':', s)) > 0) then
    Result := Copy(s, 1, p - 1);
end;

function TRSChatbox.ClickTab(tab: EChatTab; Option: String = ''): Boolean;
begin
  Result := True;

  if (Option = '') then
    Mouse.Click(FTabs[ord(tab)], mouse_Left)
  else
  begin
    Mouse.Move(FTabs[ord(tab)]);
    if (not ChooseOption.Open()) or (not ChooseOption.Select(Option)) then
      Exit(False);
  end;
end;

begin
  Chatbox.__Setup();
end;
